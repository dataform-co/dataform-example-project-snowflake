/*
   This script creates the table daily_country_order_stats under the default schema of the project (dataform by default) defined in dataform.json
   https://docs.dataform.co/guides/datasets/
*/

config {
  type: "table",
  tags: ["reporting", "daily"],
  description: "This table contains summary stats by date aggregated by country",
  columns: { date: "Date of the order",
            country: "Country of the user",
            item_count: "Number of items ordered that day",
            amount: "Total amount of revenue generated from orders that day, in US dollars using a floating FX rate"
  },
  assertions: {
    uniqueKey: ["date", "country"]
  }
}

SELECT orders.order_date      AS date,
       customers.country      AS country,
       SUM(orders.item_count) AS item_count,
       SUM(orders.amount)     AS amount

FROM ${ref('order_stats')} orders
LEFT JOIN ${ref('customers')} customers ON orders.customer_id = customers.id

GROUP BY 1, 2