config {
  type: "table",
  tags: ["reporting", "daily"],
  description: "This table contains all information about customers, bringing all orders and lineitems information about a customer into one place.",
  columns: {
    customer_key: "unique customer identifier",
    full_name: "Full name of the customer",
    address: "Customer's address",
    phone_number: "Customer phone number",
    country_name: "Customer country (full name)",
    country_group: "Customer country group",
    order_count: "Lifetime number of orders made by the customer",
    total_spent: "Lifetime customer spend"
  },
  assertions: {
    nonNull: ["full_name", "customer_key", "country_name"],
    uniqueKey: ["customer_key"]
  }
}

select
  customers.customer_key,
  customers.full_name,
  customers.address,
  customers.phone_number,
  customers.country_name,
  ${common.countryGroup("customers.country_name")} as country_group,
  count(orders.order_key) as order_count,
  sum(orders.total_price) as total_spent
from
  ${ref("STG_CUSTOMER")} customers
  left join ${ref("STG_ORDERS")} orders 
    on customers.customer_key = orders.customer_key
where
  customers.customer_key is not null
  and customers.full_name <> 'internal account'
group by
  1,2,3,4,5,6
